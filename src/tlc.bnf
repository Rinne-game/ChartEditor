譜面ファイル ::= lane行 ブロック+

ブロック ::= レイヤーブロック | ギミック行 | レイヤー非依存ブロック

# ===== レイヤー依存 =====
レイヤーブロック ::= レイヤー行 ノーツ行+

レイヤー行 ::= "#layer:" レイヤー番号 "\n"

ノーツ行 ::= 時刻 "|" ノーツリスト "|"  (Length部)? (ギミック行)* "\n"

ノーツリスト ::= ノーツ ("," ノーツ)*

ノーツ ::= "-N" | "-T" | "-F" | "SL" | "SR" | "-H"

# ホールド用Length部（オプション）
Length部 ::= "<<Length:[" 長さリスト "]"
長さリスト ::= 長さ要素 ("," 長さ要素)*
長さ要素 ::= 浮動小数 | 空文字

時刻 ::= 浮動小数

# ===== ギミック =====
ギミック行 ::= スクロールギミック | スピードギミック

スクロールギミック ::= "G:Scroll=" 浮動小数 "\n"

スピードギミック ::= "<<Speed:[" 時刻範囲 "]=" 浮動小数 ">" 浮動小数 "\n"

時刻範囲 ::= 浮動小数 "~" 浮動小数

# ===== レイヤー非依存ブロック =====
レイヤー非依存ブロック ::= "##" ブロック名 "\n" 非依存ブロック内容

ブロック名 ::= 識別子
非依存ブロック内容 ::= 非依存行+

非依存行 ::= 時刻 "|" "[" 浮動小数リスト "]" "\n"   # lane_move用
           | 任意の行内容                              # 将来追加用

浮動小数リスト ::= 浮動小数 ("," 浮動小数)*

識別子 ::= [a-zA-Z_][a-zA-Z0-9_]*

浮動小数 ::= 整数部 ("." 小数部)?
整数部   ::= [0-9]+
小数部   ::= [0-9]+
